lista # lista.0
portal # portal.0

{
  compara_listas: args =>
    args[0][.] != args[1][.] ? 0 :
    args[0][.] == 0 ? 1 :
    args[0][0] != args[1][0] ? 0 :
    compara_listas({args[0][1:] args[1][1:]})
  exibir: resultado =>
    portal[0]({
      ...lista.mapear({
        resultado.mensagens
        mensagem => portal.escreva(mensagem)
      })
      portal.escreva(resultado.passaram + "/" + resultado.mensagens[.] + " testes passaram.")
      resultado.passaram == resultado.mensagens[.] ?
        portal.saia(0)
      :
        portal.saia(1)
    })

  uniteste: {
    descrever: args => destino => {
      nome: args[0]
      resultados: args[1]
      resultado: {
        passaram: lista.reduzir({
          lista.mapear({
            resultados
            resultado => (resultado("descrever")).passaram
          })
          args => args[0] + args[1]
        })
        mensagens: lista.planificar(lista.mapear({
          resultados
          resultado => lista.mapear({
            (resultado("descrever")).mensagens
            mensagem => {
              nome
              " | "
              mensagem
            } * ""
          })
        }))
      }
      destino == "descrever" ? resultado : exibir(resultado)
    }[0]
    iguais: args => destino => {
      resultado: {
        passaram: args[0] == args[1] ? 1 : 0
        mensagens: {
          passaram == 1 ? "ok" : {
            "Esperava que "
            "" + args[0]
            " fosse igual a "
            "" + args[1]
            "."
          } * ""
        }
      }
      destino == "descrever" ? resultado : exibir(resultado)
    }[0]
    listas_iguais: args => destino => {
      resultado: {
        passaram: compara_listas({args[0] args[1]}) ? 1 : 0
        mensagens: {
          passaram == 1 ? "ok" : {
            "Esperava que {"
            lista.juntar({args[0] ", "})
            "} fosse igual a {"
            lista.juntar({args[1] ", "})
            "}."
          } * ""
        }
      }
      destino == "descrever" ? resultado : exibir(resultado)
    }[0]
  }
}.uniteste